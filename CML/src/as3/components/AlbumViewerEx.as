package as3.components
{
	import as3.components.ui.InfoPanel;
	import as3.components.ui.ViewerMenu;
	import com.gestureworks.cml.components.AlbumViewer;
	import com.gestureworks.cml.elements.Album;
	import com.gestureworks.cml.elements.Frame;
	import com.gestureworks.cml.elements.Image;
	import com.gestureworks.cml.utils.DisplayUtils;
	import com.gestureworks.core.GestureWorks;
	import com.gestureworks.utils.ExampleTemplate;
	
	[SWF(width="1280",height="720",backgroundColor="0x000000",frameRate="60")]
	
	public class AlbumViewerEx extends GestureWorks
	{		
		public function AlbumViewerEx():void
		{
			gml = "gml/gestures.gml";
		}
		
		override protected function gestureworksInit():void
		{
			//Description
			var exTemp:ExampleTemplate = new ExampleTemplate();
			exTemp.createTitle("AlbumViewer");
			exTemp.createDesc("<p>The AlbumViewer is a component that is primarily meant to display an Album on the front side and meta-data on the back side. It is composed of the following elements: "
			+"album, front, back, menu, and frame. The width and height of the component are automatically set to the dimensions of the album unless it is previously specifed by the component.<br /><br />" 
			+ "This example demonstrates the horizontal scrolling of a of an album within a viewer frame and an associated info panel. The viewer can be rotated, scaled, and dragged.</p>" 
			+ "<br /><p>See also:<br />Component<br />Album<br /></p>");
			addChild(exTemp);
			
			//Album Viewer Component
			var albumViewer:AlbumViewer = new AlbumViewer();
			albumViewer.clusterBubbling = true;
			albumViewer.x = 550;
			albumViewer.y = 280;
			albumViewer.rotation = -20;
			albumViewer.mouseChildren = true;
			albumViewer.gestureList = {"2-finger-drag": true, "n-scale": true, "n-rotate": true};
			addChild(albumViewer);
			
			//FRONT: album element			
			var album:Album = new Album();
			album.addChild(getImage("assets/images/USS_Macon_over_Manhattan.png"));
			album.addChild(getImage("assets/images/wb3.jpg"));
			album.addChild(getImage("assets/images/USS_Macon_over_Manhattan.png"));
			album.loop = true;
			album.dragGesture = "1-finger-drag";
			albumViewer.addChild(album);
			
			//BACK: info panel
			var infoPanel:InfoPanel = new InfoPanel();
			infoPanel.bkgColor = 0x6699FF;
			infoPanel.tFontSize = 20;
			infoPanel.dFontSize = 16;
			infoPanel.title = "The USS Macon";
			infoPanel.descr = "Macon, built in Ohio by the Goodyear-Zeppelin Corporation, was commissioned on 11 Mar 1933. 785 feet long and 132 feet in diameter, the airship carried five Curtiss F9C Sparrowhawk biplanes and had a crew of 76.";
			albumViewer.addChild(infoPanel);
			albumViewer.back = infoPanel;
			
			//Frame
			var frame:Frame = new Frame();
			frame.targetParent = true;
			frame.mouseChildren = false;
			albumViewer.addChild(frame);
			
			// Menu
			var menu:ViewerMenu = new ViewerMenu();
			menu.paddingLeft = 375;
			menu.paddingRight = 20;
			albumViewer.addChild(menu);
			
			//Initialize component
			DisplayUtils.initAll(albumViewer);
		}
		
		/**
		 * Returns an image element generated by the external source
		 * @param	source  the source file
		 * @return
		 */
		private function getImage(source:String):Image
		{
			var img:Image = new Image();
			img.src = source;
			img.width = 500;
			img.height = 300;
			img.init();
			return img;
		}
	}

}